programa:
      programa instruccion
    | instruccion
    ;

instruccion:
      declaracion_estudiante
    | declaracion_cv
    | declaracion_oferta
    | mostrar_cv
    | evaluar_cv
    | condicion_generacion
    ;

declaracion_estudiante:
    ESTUDIANTE IDENTIFICADOR LLAVE_APERTURA lista_atributos_estudiante LLAVE_CIERRE PUNTOYCOMA
    ;

lista_atributos_estudiante:
      atributo_estudiante
    | lista_atributos_estudiante COMA atributo_estudiante
    ;

atributo_estudiante:
      NOMBRE DOSPUNTOS CADENA
    | CARRERA DOSPUNTOS CADENA
    | CONTACTO DOSPUNTOS LLAVE_APERTURA contacto LLAVE_CIERRE
    ;

contacto:
      EMAIL DOSPUNTOS CADENA COMA TELEFONO DOSPUNTOS CADENA
    ;

declaracion_cv:
    CV IDENTIFICADOR LLAVE_APERTURA lista_atributos_cv LLAVE_CIERRE PUNTOYCOMA
    ;

lista_atributos_cv:
      atributo_cv
    | lista_atributos_cv COMA atributo_cv
    ;

atributo_cv:
      REFERENCIA_ESTUDIANTE DOSPUNTOS IDENTIFICADOR
    | EXPERIENCIA_LISTA DOSPUNTOS CORCHETE_APERTURA lista_experiencias CORCHETE_CIERRE
    ;

lista_experiencias:
      experiencia
    | lista_experiencias COMA experiencia
    ;

experiencia:
    EXPERIENCIA LLAVE_APERTURA lista_campos_experiencia LLAVE_CIERRE
    ;

lista_campos_experiencia:
      campo_experiencia
    | lista_campos_experiencia COMA campo_experiencia
    ;

campo_experiencia:
      EMPRESA DOSPUNTOS CADENA
    | PUESTO DOSPUNTOS CADENA
    | LOGROS_LISTA DOSPUNTOS CORCHETE_APERTURA lista_cadenas CORCHETE_CIERRE
    ;

lista_cadenas:
      CADENA
    | lista_cadenas COMA CADENA
    ;

declaracion_oferta:
    OFERTA IDENTIFICADOR LLAVE_APERTURA lista_atributos_oferta LLAVE_CIERRE PUNTOYCOMA
    ;

lista_atributos_oferta:
      atributo_oferta
    | lista_atributos_oferta COMA atributo_oferta
    ;

atributo_oferta:
      PUESTO DOSPUNTOS CADENA
    | PALABRAS_CLAVE DOSPUNTOS CORCHETE_APERTURA lista_cadenas CORCHETE_CIERRE
    | SUELDO_MIN DOSPUNTOS NUMERO
    ;

mostrar_cv:
    MOSTRAR IDENTIFICADOR PUNTOYCOMA
    ;

evaluar_cv:
    EVALUAR CV IDENTIFICADOR PARA OFERTA IDENTIFICADOR PUNTOYCOMA
    ;

condicion_generacion:
    SI condicion ENTONCES instruccion_generacion
    ;

condicion:
    IDENTIFICADOR PUNTO ATRIBUTO CONTIENE IDENTIFICADOR PUNTO ATRIBUTO
    ;

instruccion_generacion:
    GENERAR CV IDENTIFICADOR PARA OFERTA IDENTIFICADOR EN FORMATO FORMATO_ID PUNTOYCOMA
    ;
